apply plugin: 'maven-publish'
apply plugin: 'signing'

def hasToSignArtifact() {
    def signInProps = [
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"]
    ]
    return signInProps.every { e -> e }
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = 'ImageProvider'
                    description = 'The easiest way to gather images from camera, gallery, etc.'
                    url = 'https://github.com/imablanco/ImageProvider'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'ablanco'
                            name = 'Alvaro Blanco'
                            email = 'alvaro.bl91@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com:imablanco/ImageProvider.git'
                        developerConnection = 'scm:git@github.com:imablanco/ImageProvider.git'
                        url = 'https://github.com/imablanco/ImageProvider'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}

tasks.withType(Sign) {
    onlyIf { hasToSignArtifact() }
}

/*./gradlew publishReleasePublicationToSonatypeRepository --max-workers 1 closeAndReleaseSonatypeStagingRepository*/
